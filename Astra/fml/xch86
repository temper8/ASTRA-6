C XCH86[m^2/s]	neoclass. thermal ion cond. after Chang and Hinton
C
C		C.S.Chang,F.L.Hinton,Phys.Fluids 29, 3314 (1986)
C			(Alexander 08-NOV-94)
C
C	Please use only one impurity (ZIM1, NIZ1)
C	Please use ZMAIN and AMAIN as charge and rel. mass
C	of main plasma component
C	NI should be the total density of ions, including the impurity
C
	include	'fml/nuis'
	IF(J.NE.NA1) THEN	
	  GRADR=(SHIF(J+1)-SHIF(J))/HRO
	ELSE
	  GRADR=(SHIF(J)-SHIF(J-1))/HRO
	END IF

	A2   = 1.03
	B2   = 0.31
	C2   = 0.74
	XK20 = 0.66

	BPOL2 = ((BTOR*J*HRO*ABS(MU(J)))/RTOR)**2
	DELTA = SQEPS(J)**2
	ALPHA = (ZIM1(J)**2*NIZ1(J))/(ZMAIN(J)**2*(NI(J)-NIZ1(J)))
	XMUST = NUIS*(1.+1.54*ALPHA)
	XHP   = 1.+1.33*ALPHA*(1.+0.6*ALPHA)/(1.+1.79*ALPHA)
	BMEAN = (1.+1.5*(DELTA**2+DELTA*GRADR)+0.375*DELTA**3*GRADR)/
     &          (1.+0.5*DELTA*GRADR)
	BINV  = SQRT(1.-DELTA**2)*(1.+DELTA*0.5*GRADR)/
     &          (1.+(GRADR/DELTA)*(SQRT(1.-DELTA**2)-1.))
	F     = 0.5*1./SQEPS(J)*(BMEAN-BINV)
	XK2A  = (0.66*(1.+1.54*ALPHA)+(1.88*SQEPS(J)-1.54*DELTA)*
     &          (1.+3.75*ALPHA))*BMEAN
	RHO2  = 2.09E-5*AMAIN(J)/ZMAIN(J)**2*TI(J)/BPOL2
	XK2   = XK20*(XK2A/0.66/(1.+A2*SQRT(XMUST)+B2*XMUST)+
     &          (C2**2/B2*XMUST*SQEPS(J)**3)/
     &          (1.+C2*XMUST*SQEPS(J)**3)*XHP*F)
	XCH86= SQEPS(J)*RHO2*NUI*XK2

